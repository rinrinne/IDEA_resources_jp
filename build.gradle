allprojects {
     ext {
        target15Dir = new File(rootDir, 'target/15')
        target16Dir = new File(rootDir, 'target/2016')
    }
} 

subprojects {
    apply plugin: 'java'
    task prepareDir << {
        new File(projectDir, "src/main/resources").mkdir()
    }
    task removeDir(type: Delete) {
        delete new File(projectDir, "src")
    }

    task copyDistFiles(type: Copy) {
        from new File(project.buildDir, "libs")
        into new File(rootDir, "build/distribution")
    }

    task processPropertiesFile << {
        sourceSets.each { s ->
            s.resources.srcDirs.each { d ->
                if (d.isDirectory()) {
                    ant.native2ascii(
                            src: "${d}",
                            dest: "${s.output.resourcesDir}",
                            includes: '**/*.properties',
                            encoding: 'UTF-8'
                    )
                }
            }
        }
    }
    processResources.excludes = ['**/*.properties']
    processResources.dependsOn processPropertiesFile
    build.dependsOn copyDistFiles
    copyDistFiles.mustRunAfter jar
}

project(":resources-2016") {
    apply plugin: 'java'
    defaultTasks 'jar'
    jar.baseName = 'resources_ja'
    jar.appendix = '2016'

    task processResourceFiles(dependsOn: [prepareDir, '::translateResources']) << {
        ["search", "messages"].each { src ->
            copy {
                from new File(target16Dir, src)
                into new File(sourceSets.main.resources.srcDirs[0], src)
            }
        }

        ["fileTemplates", "inspectionDescriptions", "intentionDescriptions",
         "postfixTemplates", "tips"].each { src ->
            copy {
                from new File(target16Dir, src)
                into new File(sourceSets.main.resources.srcDirs[0], src + "_ja")
            }
        }
    }
    processResources.dependsOn processResourceFiles
    processPropertiesFile.dependsOn processResourceFiles
    clean.dependsOn removeDir
}

project(":resources-15") {
    apply plugin: 'java'
    defaultTasks 'jar'
    jar.baseName = 'resources_ja'
    jar.appendix = '15'

    task processResourceFiles(dependsOn: [prepareDir, '::translateResources']) << {
        ["search", "messages"].each { src ->
            copy {
                from new File(target15Dir, src)
                into new File(sourceSets.main.resources.srcDirs[0], src)
                rename '(.*).properties', '$1_ja.properties'
            }
        }
        ["fileTemplates", "inspectionDescriptions", "intentionDescriptions",
         "postfixTemplates", "tips"].each { src ->
            copy {
                from new File(target15Dir, src)
                into new File(sourceSets.main.resources.srcDirs[0], src + "_ja")
            }
        }
    }
    processResources.dependsOn processResourceFiles
    processPropertiesFile.dependsOn processResourceFiles
    clean.dependsOn removeDir
    clean.dependsOn '::cleanRootDirs'
}

def targetDir = new File(rootDir, "target")
task createTargetDir << {
    targetDir.mkdir()
}
task cleanRootDirs(type: Delete) {
    delete target15Dir
    delete target16Dir
    delete new File(rootDir, "build")
}

task translateResources(type: Exec, dependsOn: createTargetDir) {
    workingDir rootDir
    commandLine 'omegat', rootDir, '--mode=console-translate'
    inputs.dir new File(rootDir, "source")
    outputs.dir targetDir
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}
